{-
  Contract handles internal messages with queries with the following scheme
  `_# score:uint32 value:(VarUInteger 16) msg:^Cell = MsgInternalBody`, where msg contains message body
  which shoud be sent later and store it to contract.
  Once the number of stored queries reaches 12, contract should send and delete from storage message with
  the highest score and message with the lowest value (if it is the same message, it should be sent once).
  Messages should be sent to any address with mode 0, coin amount should be equal to value and it should contain
  corresponding message body. All scores and values are guaranteed to be different
  Note, that in addition to gas-fees, storage fees will be used to determine final score. In particular,
  storage fee will be calculated like between each message passes 3 days (259200 seconds).
  Gas-units price and storage fee params will correspond to current configs of masterchain:
  1000 nanoTON per 65536 bits per second + 500000 nanoTON per 65536 cells per second; gas is 10000 nanoTON per unit.


  Example:
  (message with score x and value y are represented as `(x,y)` )

  incoming message   outcoming messages     
  (1, 5)           | -
  (2, 6)           | -
  (3, 100)         | -
  (4, 2)           | -
  (5, 3)           | -
  (6, 4)           | -
  (7, 7)           | -
  (8, 8)           | -
  (9, 9)           | -
  (10, 10)         | -
  (11, 11)         | -
  (12, 20)         | (12,20); (4,2)
  (15, 1)          | -
  (13, 13)         | (15, 1)
  (14, 14)         | (14,14); (5,3)
-}

;; =============== common utils =============================

(slice, (int, int, cell)) unpack(slice sl) inline {
    int score = sl~load_uint(32);
    int value = sl~load_coins();
    cell msg = sl~load_ref();
    return (sl, (score, value, msg));
}

(builder) pack(builder bl, (int, int, cell) value) inline {
    (int score, int value, cell msg) = value;
    return bl.store_uint(score, 32).store_coins(value).store_ref(msg);
}

(slice, int) read_current_number() inline {
    slice storage_sl = get_data().begin_parse();
    if (storage_sl.slice_empty?()) {    ;; todo optimization: cell_empty (no need to parse empty cell)
        return (storage_sl, 0);
    }
    return storage_sl.load_uint(4);
}

builder write_current_number(int current_number) inline {
    return begin_cell().store_uint(current_number, 4);
}

(slice, int) read_node_num(slice node_sl) inline {
    int node_num = node_sl~load_uint(2) + 1;
    return (node_sl, node_num);
}

(builder) write_node_num(builder node_bl, int node_num) inline {
    return node_bl.store_uint(node_num - 1, 2);
}

;; =============== add_message =============================

() add_message(slice storage_sl, int current_number, (int, int, cell) tupled_msg) impure inline {
    builder storage_bl = write_current_number(current_number + 1);
    if (current_number < 4) {
        ;; 1, 2 or 3
        repeat (3) {
            ifnot (storage_sl.slice_refs_empty?()) {
                storage_bl = storage_bl.pack(storage_sl~unpack());
            }
        }
        storage_bl = storage_bl.pack(tupled_msg);
        storage_bl.end_cell().set_data();
    } else {
        if (current_number < 7) {
            ;; 5, 6
            storage_bl = storage_bl.store_ref(storage_sl~load_ref());    ;; first node as is
            repeat (2) {
                ifnot (storage_sl.slice_refs_empty?()) {
                    storage_bl = storage_bl.pack(storage_sl~unpack());
                }
            }
            storage_bl = storage_bl.pack(tupled_msg);
            storage_bl.end_cell().set_data();
        } else {
            if (current_number < 10) {
                ;; 8, 9
                storage_bl = storage_bl.write_node_num(4).write_node_num(4);  ;; number of messages in nodes
                storage_bl = storage_bl.store_ref(storage_sl~load_ref());    ;; first node as is
                storage_bl = storage_bl.store_ref(storage_sl~load_ref());    ;; second node as is

                int third_num = 1;
                slice third_sl = storage_sl~load_ref().begin_parse();
                builder third_bl = begin_cell();
                ;; possible 9th message
                ifnot (third_sl.slice_refs_empty?()) {
                    third_bl = third_bl.pack(third_sl~unpack());
                    third_num += 1;
                }
                ;; new message (9th or 10th)
                third_bl = third_bl.pack(tupled_msg);

                storage_bl = storage_bl.write_node_num(third_num).store_ref(third_bl.end_cell());
                storage_bl.end_cell().set_data();
            } else {
                if (current_number < 12) {
                    ;; 10, 11 - check unfilled nodes

                    int packed = false;
                    repeat (3) {
                        int node_num = storage_sl~read_node_num();
                        if ((node_num < 4) & (~ packed)) {
                            slice node_sl = storage_sl~load_ref().begin_parse();
                            builder node_bl = begin_cell();
                            repeat (node_num) {
                                node_bl = node_bl.pack(node_sl~unpack());
                                packed = true;
                            }
                            node_bl = node_bl.pack(tupled_msg);
                            storage_bl = storage_bl.write_node_num(node_num + 1).store_ref(node_bl.end_cell());
                        } else {
                            storage_bl = storage_bl.write_node_num(node_num).store_ref(storage_sl~load_ref());
                        }
                    }

                    storage_bl.end_cell().set_data();
                } else {
                    throw(666);
                }
            }
        }
    }
}

;; =============== reformat =============================

() reformat(slice storage_sl, int current_number, (int, int, cell) tupled_msg) impure inline {
    builder storage_bl = write_current_number(current_number + 1);
    if (current_number == 0) {
        storage_bl.pack(tupled_msg).end_cell().set_data();
    } else {
        if (current_number == 4) {
            ;; cell first = begin_cell().store_slice(storage_sl).end_cell();
            builder first_bl = begin_cell();
            repeat (4) {
                first_bl = first_bl.pack(storage_sl~unpack());
            }
            cell first = first_bl.end_cell();
            storage_bl.store_ref(first)
                    .pack(tupled_msg)
                    .end_cell().set_data();
        } else {
            if (current_number == 7) {
                cell first = storage_sl~load_ref();
                builder second_bl = begin_cell();
                repeat (3) {
                    second_bl = second_bl.pack(storage_sl~unpack());
                }
                cell second = second_bl.pack(tupled_msg).end_cell();
                cell third = begin_cell().end_cell();
                storage_bl.store_ref(first).store_ref(second).store_ref(third)
                        .end_cell().set_data();
            }
        }
    }
}

;; =============== extract_and_send =============================
;; =============== extract_and_send: find_max_min =============================

(slice, int, int, int, int) compare_values(
        slice sl, int max_score, int max_value, int min_value, int same_message) inline {
    (int score, int value, _) = sl~unpack();
    int new_same = 0;
    if (score > max_score) {
        max_score = score;
        max_value = value;
        new_same += 1;
    }
    if (value < min_value) {
        min_value = value;
        new_same += 1;
    }
    if (new_same == 1) {
        same_message = false;
    } else {
        if (new_same == 2) {
            same_message = true;
        }
    }
    return (sl, max_score, max_value, min_value, same_message);
}

(int, int, int, int) find_max_min() inline {
    slice storage_sl = get_data().begin_parse();
    slice node_sl = storage_sl~load_ref().begin_parse();
    (int max_score, int min_value, _) = node_sl~unpack();
    int max_value = min_value;
    int same_message = true;

    repeat (3) {
        repeat (4) {
            ifnot (node_sl.slice_refs_empty?()) {
                (node_sl, max_score, max_value, min_value, same_message) =
                        compare_values(node_sl, max_score, max_value, min_value, same_message);
            }
        }
        ifnot (storage_sl.slice_refs_empty?()) {
            node_sl = storage_sl~load_ref().begin_parse();
        }
    }

    return (max_score, max_value, min_value, same_message);
}

;; =============== extract_and_send: remove_max_min_one =============================

(cell, int, cell) remove_max_min_node_one(cell node, int node_num, int max_score, int min_value) inline {
    slice node_sl = node.begin_parse();
    builder node_bl = begin_cell();
    cell max_min_msg = null();
    repeat (node_num) {
        (int, int, cell) tupled_msg = node_sl~unpack();
        (int score, int value, cell msg) = tupled_msg;
        if ((score == max_score) & (value == min_value)) {
            max_min_msg = msg;
            ;; node_bl pack nothing
            node_num -= 1;
        } else {
            node_bl = node_bl.pack(tupled_msg);
        }
    }
    return (node_bl.end_cell(), node_num, max_min_msg);
}

(cell) remove_max_min_one(int max_score, int min_value) impure inline {
    cell max_min_msg = null();

    builder storage_bl = write_current_number(11);
    (slice storage_sl, _) = read_current_number();
    repeat (3) {
        int node_num = storage_sl~read_node_num();
        cell node = storage_sl~load_ref();
        if (max_min_msg.null?()) {
            (node, node_num, max_min_msg) = remove_max_min_node_one(node, node_num, max_score, min_value);
            storage_bl = storage_bl.write_node_num(node_num).store_ref(node);
        } else {
            storage_bl = storage_bl.write_node_num(node_num).store_ref(node);
        }
    }

    storage_bl.end_cell().set_data();
    return (max_min_msg);
}

;; =============== extract_and_send: remove_max_min_two =============================

(cell, int, cell, cell) remove_max_min_node_two(
        cell node, int node_num, cell max_msg, cell min_msg, int max_score, int min_value) inline {
    slice node_sl = node.begin_parse();
    builder node_bl = begin_cell();
    repeat (node_num) {
        (int, int, cell) tupled_msg = node_sl~unpack();
        (int score, int value, cell msg) = tupled_msg;
        if (score == max_score) {
            max_msg = msg;
            ;; node_bl pack nothing
            node_num -= 1;
        } else {
            if (value == min_value) {
                min_msg = msg;
                ;; node_bl pack nothing
                node_num -= 1;
            } else {
                node_bl = node_bl.pack(tupled_msg);
            }
        }
    }
    return (node_bl.end_cell(), node_num, max_msg, min_msg);
}

(cell, cell) remove_max_min_two(int max_score, int min_value) impure inline {
    ;; remove max node, drop min node
    (cell max_msg, cell min_msg) = (null(), null());

    builder storage_bl = write_current_number(10);
    (slice storage_sl, _) = read_current_number();
    repeat (3) {
        int node_num = storage_sl~read_node_num();
        cell node = storage_sl~load_ref();
        if ((max_msg.null?()) | (min_msg.null?())) {
            (node, node_num, max_msg, min_msg) = remove_max_min_node_two(
                    node, node_num, max_msg, min_msg, max_score, min_value);
            storage_bl = storage_bl.write_node_num(node_num).store_ref(node);
        } else {
            storage_bl = storage_bl.write_node_num(node_num).store_ref(node);
        }
    }

    storage_bl.end_cell().set_data();
    return (max_msg, min_msg);
}

;; =============== extract_and_send: send_message =============================

() send_message(cell msg, int value) impure inline {
;;     ~dump(value);
;;     slice tmp = msg.begin_parse();
;;     ~dump(tmp);
;;     ~dump(tmp~load_int(17));

    cell msg_to_sent = begin_cell()
            .store_uint(0x18, 6) ;; tech flags
            .store_slice(my_address())   ;; destination address
            .store_coins(value) ;; attached value
            ;; store inplace
;;             .store_uint(0, 107) ;; more tech flags :)
;;             .store_slice(msg.begin_parse())
            ;; strore as a reference
            .store_uint(1, 107) ;; more tech flags :)
            .store_ref(msg)
            .end_cell();

    send_raw_message(msg_to_sent, 0);
}

;; =============== extract_and_send: extract_and_send =============================

() extract_and_send() impure inline {
    (int max_score, int max_value, int min_value, int same_message) = find_max_min();
    if (same_message) {
        cell max_min_msg = remove_max_min_one(max_score, min_value);
        send_message(max_min_msg, min_value);
    } else {
        (cell max_msg, cell min_msg) = remove_max_min_two(max_score, min_value);
        send_message(max_msg, max_value);
        send_message(min_msg, min_value);
    }
}

;; =============== recv_internal =============================

;; testable
() recv_internal (slice in_msg_body) impure {
    (int, int, cell) tupled_msg = in_msg_body~unpack();
    ;; load number of current messages
    ;; if new can be added without structure reformat - add
    ;; else - reformat structure
    ;; 1-4 cells - directly in storage cell
    ;; 5-7 cells - first 4 in first parent cell; others directly in storage cell
    ;; 8-12 cells - first 4 in first parent cell; second 4 in second parent cell; third 4 in third parent cell
    (slice storage_sl, int current_number) = read_current_number();
    if ((current_number == 0) | (current_number == 4) | (current_number == 7)) {
        reformat(storage_sl, current_number, tupled_msg);
    } else {
        add_message(storage_sl, current_number, tupled_msg);
        if (current_number == 11) {
            extract_and_send();
        }
    }
}
